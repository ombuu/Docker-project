plugins {
  id 'java'
  id 'org.springframework.boot' version '2.6.2'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'com.google.cloud.tools.jib' version '3.1.4'
  id 'org.sonarqube' version '3.3'
  id 'jacoco'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// 젠킨스를 통해 빌드를 진행할 때, spring 웹 앱을 컴파일 빌드 하기 위해 nexus-artifact-repository를 url로 설정해서 이브러리나 종속성, plugin들을 가져올수 있도록 설정
// http이므로 allowInsecureProtocol true 상태로 설정
repositories {
    maven {
        url "http://172.31.3.122:8081/repository/nexus-artifact-repository"
        allowInsecureProtocol true
    }

}

version = '0.0.1-SNAPSHOT'
description = 'update-docker-mini-project'
group = 'com.test'

java.sourceCompatibility = JavaVersion.VERSION_11

jar {
	enabled = false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()
}

// jacoco를 통해서 기본적인 정적 분석에 대한 스캔을 진행하고, 
// 코드의 품질에 대한 기반을 마련할 수 있는 부분에 대해서도 명시를 해놔서 reporting을 할 수 있도록 설정
jacoco{
  toolVersion = "0.8.7"
}

jacocoTestReport {
 reports{
  html.enabled=true
  xml.enabled=true
  csv.enabled=true
 }
}

//jacoco를 기반으로 리포팅이 만들어진것을 sonarqube를 통해 리포팅 결과를 출력하고, 어떤 부분에서 품질에 문제가 되는지 확인
sonarqube {
  properties {
      property "sonar.projectName","update-docker-mini-project"
      property "sonar.exclusions", "**/generated-*/**/*"
      property "sonar.projectKey", "org.sonarqubeJacocoCodeCoverage"
      property "sonar.reportPath" , "${project.buildDir}/jacoco/test.exec"
      property "sonar.host.url", "http://172.31.3.122:9000"
      property "sonar.sources", "src/main/java"
      property "sonar.tests", "src/test/java"
      property "sonar.login", "test"
      property "sonar.password", "!test00"
  }
}

tasks['sonarqube'].dependsOn test


jib {
  from {
    image = "172.31.3.122:5000/test:base-image"
  }
  allowInsecureRegistries = true
	container {
    entrypoint = ['java', '-Dspring.profiles.active=test', '-jar', 'update-docker-mini-project-0.0.1-SNAPSHOT.jar']
    // mainClass = 'com.test.StartApplication'
    jvmFlags = ['-Xms512m', '-Xmx512m', '-Xdebug', '-XshowSettings:vm', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseContainerSupport']
		ports = ['8080']

    environment = [SPRING_OUTPUT_ANSI_ENABLED: "ALWAYS"]
    labels = [version:project.version, name:project.name, group:project.group]

    creationTime = 'USE_CURRENT_TIMESTAMP'
    format = 'Docker'
	}
  extraDirectories {
    paths {
      path {
        from = file('build/libs')
      }
    }
  }
}